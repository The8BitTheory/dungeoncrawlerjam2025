#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0,uppercase,10,10
# MAP GENERATOR
# DATA FORMAT

#  1 BYTE: OFFSET TO MAPDATA
#  1 BYTE: WIDTH TILES
#  1 BYTE: HEIGHT TILES
#  2 BYTES: NR GEMS TO COLLECT

# THE FOLLOWING VALUES CAN BE IN ANY ORDER.
#  FIRST BYTE IS USED TO QUALIFY THE DATA (6,B,8,X)

#  3 BYTES: SPAWN POINT PLAYER
#  3 BYTES: SPAWN POINT GHOST
#  5 BYTES: TELEPORT FIELDS (2 BYTES EACH)
#  3 BYTES: EXIT

5 DD=PEEK(186)

10 DEF FN MV(ZZ)=ZZ-48
11 DEF FN LB(ZZ)=DEC(RIGHT$(HEX$(ZZ),2))
12 DEF FN HB(ZZ)=DEC(LEFT$(HEX$(ZZ),2))

20 BE=PEEK(4624)+PEEK(4625)*256
21 ME=0
22 MN$="CRAWL128"

27 W%=20:H%=11

#  VO:VALUE-OFFSET (HELPER VARIABLE FOR POKE)
55 MP%(0)=W%*2:MP%(1)=H%:MP%(2)=MP%(0)*MP%(1):VO=5:GS=0

62 BANK 0
63 FOR X=0TO W%*H%-1:REM 28*MP%(1)-1:REM 11*20-1
64  READ MV$
65  IF MV$="2" THEN GS=GS+1:GOTO79

66  IF MV$="6" THEN GOSUB50000:GOTO79
67  IF MV$="8" THEN GOSUB50000:GOTO79
68  IF MV$="B" THEN GOSUB50000:GOTO79
72  IF MV$="X" THEN GOSUB50000

#75  MV=FNMV(ASC(MV$))
#76  POKE BE+100+X,MV
79 NEXT
80 ME=X-1

100 POKE BE,VO
101 POKE BE+1,W%
102 POKE BE+2,H%
103 POKE BE+3,FNLB(GS)
104 POKE BE+4,FNHB(GS)

110 RESTORE
120 FOR X=0TOME
130  READ MV$
140  MV=FNMV(ASC(MV$))
150  POKE BE+VO,MV
160  VO=VO+1
170 NEXT

179 FAST
180 FOR X=1 TO LEN(MN$)
190  POKE BE+VO,ASC(MID$(MN$,X,1))
200  VO=VO+1
210 NEXT
211 SLOW

300 PRINT "READY TO SAVE. INSERT DISK IN DRIVE #"DD
305 POKE 208,0:GETKEY I$
310 BSAVE ("@"+MN$+".CD1"),U(DD),B0,P(BE) TO P(BE+VO)



49998 BANK15
49999 END

50000 POKE BE+VO,ASC(MV$):VO=VO+1
50010 POKE BE+VO,FNLB(X):VO=VO+1
50020 POKE BE+VO,FNHB(X):VO=VO+1
50030 RETURN


# MAP DATA
# ODD NUMBERS ARE NOT-WALKABLE
# 1 - REGULAR WALL
# 3 - WALL OF MONSTER DEN
# 5 - DOOR OF MONSTER DEN
#  IT'S ABOUT THE LOWEST BIT OF THE ASCII CODE, NOT THE HEX-REP. SO, A IS ODD (65)

# EVEN NUMBERS ARE
# 0 - EMPTY
# 2 - COIN
# 4 - POWER COIN
# 6 - SPAWN POINT
# 8 - TELEPORT TO SAME CHAR
# 
# B - SPAWNPOINT OF GHOST 1 (20) (B IS NOT HEX-B BUT SOME EVEN ASCII CODE)
# X - DISABLED EXIT (VALUE IS 40)
# Z - ENABLED EXIT (VALUE IS 42)

60000 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
60001 DATA 1,2,2,2,2,1,2,2,2,X,2,2,2,2,1,2,2,2,2,1
60002 DATA 1,2,1,1,2,1,2,1,1,1,1,1,1,2,1,2,1,1,2,1
60003 DATA 1,2,1,2,2,2,2,2,B,0,0,0,2,2,2,2,2,1,4,1
60004 DATA 1,2,1,2,1,1,2,1,1,1,1,1,1,2,1,1,2,1,2,1
60005 DATA 1,2,2,2,2,2,2,1,1,1,1,1,1,2,2,2,2,2,2,1
60006 DATA 1,2,1,2,1,1,2,1,1,1,1,1,1,2,1,1,2,1,2,1
60007 DATA 1,4,1,2,2,2,2,2,2,2,2,2,2,2,2,2,2,1,2,1
60008 DATA 1,2,1,1,2,1,2,1,1,1,1,1,1,2,1,2,1,1,2,1
60009 DATA 1,2,2,2,2,1,2,2,2,2,2,2,2,2,1,2,2,2,6,1
60010 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1

