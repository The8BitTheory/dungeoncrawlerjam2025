#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC,uppercase,10,10
0 REM ** FI G,X,Y
1 PRINT "INSERT DISK AND PRESS KEY TO CONTINUE":GETKEY I$
10 FD%=0:PP%=35:REM PP%=109

30 GRAPHIC0:GOSUB 10000

40 REM CL=CHECKLIST:RL=RENDERLIST:M=MAP:VL=VIEWLIST:TL%() TELEPORT LOCATIONS

# GS%():GAMESTATE
#       1=COINS LEFT TO COLLECT

50 W=28:DIM RL%(5,2),M%(28*31-1),V%(3,19),P%(7),GS%(1),TL%(1),MP%(2)
51 TL=0:MP%(0)=56:MP%(1)=31:MP%(2)=MP%(0)*MP%(1)


#  MAP DATA
60 GS%(1)=0:REM FAST

#  CLEAR VRAM OF MAP
61 ::VMF MA,0,MP%(2)
62 FOR X=0TO28*MP%(1)-1:REM 11*20-1
63  READ MV$:IF MV$="2" THEN GS%(1)=GS%(1)+1:GOTO66
65  IF MV$="6" THEN PP%=X:ELSE IF MV$="8" THEN TL%(TL)=X:TL=TL+1
66  MV=ASC(MV$)-48
68  M%(X)=MV:IF (MVAND1)=1 THEN MV=FNCL(1):ELSEMV=255
69  C1=MA+X+X
70  ::VMF C1,MV,2
72 NEXT


#  NAVIGATION POSITION UPDATE DATA
75 FOR X=0TO7:READ MV:P%(X)=MV:NEXT


#  FACING NORTH
80 L=-1:R=-L:F=-W:B=-F:D=0:GOSUB 90
#  FACING EAST
81 L=-W:R=-L:F=1:B=-F:D=1:GOSUB 90
#  FACING SOUTH
82 L=1:R=-1:F=W:B=-F:D=2:GOSUB 90
#  FACING WEST
83 L=W:R=-L:F=-1:B=-F:D=3:GOSUB 90

84 GOTO 100


#  DIRECTION MAPPING COORDINATES
90 V%(D,1)=L:V%(D,2)=R
91 V%(D,3)=L+F:V%(D,4)=F:V%(D,5)=R+F
92 V%(D,6)=V%(D,3):V%(D,7)=V%(D,5)
93 V%(D,8)=L+F+F:V%(D,9)=V%(D,8):V%(D,10)=F+F:V%(D,11)=F+F+R:V%(D,12)=V%(D,11)
94 V%(D,13)=F+F+L:V%(D,14)=F+F+R
95 V%(D,15)=L+F+F+F:V%(D,16)=V%(D,15):V%(D,17)=F+F+F:V%(D,18)=F+F+F+R:V%(D,19)=V%(D,18)
96 RETURN


# BUILD ARRAY FOR RENDERLIST
100 FOR X=0TO5:FORY=0TO2:READMV:RL%(X,Y)=MV:NEXT:NEXT


110 TD$="WWEWWEWWQWWQWWQWWWWWQAAAAAEAAWWWWWQWWWQ"

#   WRITE FACING DIRECTION
220 GOSUB 610

#   WRITE NR OF COINS LEFT TO COLLECT
225 GOSUB 602

#   RENDER INITIAL VIEW
230 FD%=0:GOSUB 340:TX=1:S2=TI

240 DO
241  SLOW
#242  I$=MID$(TD$,TX,1):TX=TX+1
242  GETKEY I$
243  I=ASC(I$)
244  FAST

#   LOOK LEFT/RIGHT
250 IF I$="Q" THEN TU%=-1:FD%=FD%-1:IF FD%<0 THEN FD%=3:GOTO300
260 IF I$="E" THEN TU%=-1:FD%=FD%+1:IF FD%>3 THEN FD%=0:GOTO300

#   MOVE FORWARD/BACKWARD
270 IF I$="W" THEN T=FNPP(0):GOTO 520
280 IF I$="S" THEN T=FNPP(1):GOTO 520

#   STRAFE LEFT/RIGHT
282 IF I$="A" THEN T2=FD%:FD%=FNFD(3):T=FNPP(0):FD%=T2:GOTO 520
284 IF I$="D" THEN T2=FD%:FD%=FNFD(5):T=FNPP(0):FD%=T2:GOTO 520

#   DISPLAY MAP
286 IF I$="M" THEN GOSUB 1000:GOTO320

290 IF I$="X" THEN SLOW:GRAPHIC0:GOTO330

300 GOSUB 340

#   TEXT UPDATE FLAG.
301 IF TU% THEN GOSUB 610

320 LOOP UNTIL TX=LEN(TD$)

330 S2=TI-S2:PRINT "TOOK "S2 " JIFFIES":END

#1175 UNCOMPILED WITH INTEGERS
#1080 UNCOMPILED WITH REDUCED NR OF PARAMETERS
#1125 UNCOMPILED WITH AND1 FOR MOVEMENT CHECK
#1071 UNCOMPILED WITH GOTO300 FOR INPUT CHECK
#1024 UNCOMPILED WITH S REMOVED FROM RENDERING
#1021 REMOVED F% FROM TU RENDERING

#536 COMPILED WITH INTEGERS

#1186

340 C1=AP+400

#   TILE-RENDER ANALYSIS
350 ::VMF C1,0,7600

#   HERE-LEFT AND RIGHT
360 GX=2:RL=0:GOSUB 620
365 RL=1:GOSUB 620

370 GOSUB 410:REM GOTO HERE-CENTER

374 C1=CA+32*18

375 IF T4%<>2 THEN 385
379 C2=AP+80*80+40
380 ::VMC C1,C2,3,6,80:T4%=0

385 IF TA%<>2 THEN RETURN
389 C2=AP+80*64+40
390 ::VMC C1,C2,3,6,80:TA%=0

399 RETURN

#   HERE-CENTER
410 T4%=FNM(4):IF T4%=1 THEN DV=3:GOSUB710:RETURN
420 RL=2:GOSUB620
430 RL=3:GOSUB620
440 GOSUB470:REM GOTO NEAR-CENTER
450 RETURN

#   NEAR-CENTER
470 TA%=FNM(10):IF TA%=1 THEN DV=9:GOSUB710:RETURN
480 GX=1:RL=4:GOSUB620
490 RL=5:GOSUB620

#   FAR CENTER
500 IF FNM(17)=1 THEN DV=16:GOSUB710

510 RETURN

######################
# MOVEMENT UPDATE
# INCLUDING TELEPORTERS, WALLS, ...
########################
520 V%=M%(T):IF (V% AND 1) THEN 300
522 PP%=T
523 IF M%(PP%)=2 THEN GOSUB 600

524 IF V%=8 THEN PP%=TL%(ABS(PP%=TL%(0)))

530 GOTO 290

#############################
#   UPDATE COINS LEFT TO COLLECT
#####
600 M%(PP%)=0:GS%(1)=GS%(1)-1
602 TX$=MID$(STR$(GS%(1))+" ",2)
604 FOR X=1 TO LEN(TX$)
605  C1=CA+FN TX(X)*18
606  C2=AP+15+3*(X-1)
607  ::VMC C1,C2,3,6,80
608 NEXT:RETURN

#####################
# UPDATE COMPASS
####################

610 TU%=0:TX$=FD$
611 C1=CA+FNTX(FD%+1)*18:C2=AP+3
612 ::VMC C1,C2,3,6,80
614 RETURN

# ADD VALUE TO RENDERLIST
620 FOR G=0 TO GX
640  DV=RL%(RL,G):IF FNM(DV)=1 THEN DV=DV-1:GOSUB710:GOTO 700
690 NEXT
700 RETURN

#   RENDER FRAME
710 C1=DA+DM%(DV,0):C2=AP+DM%(DV,4):C3=DM%(DV,1):C4=DM%(DV,2)
711 ::VMC C1,C2,C3,C4,80
712 RETURN

# WRITE TEXT
800 FOR X=1TOLEN(TX$):F=FN TX(X)
809  C1=CA+F*18:C2=VR(4)+3*(X-1)+TD
810  ::VMC C1,C2,3,6,80
820 NEXT
830 RETURN



###############
# DISPLAY LEVEL MAP
###############

1000 C1%=20*80+12:C2%=C1%-322

#    CREATE FRAME FOR MAP
1010 FOR X=0TO39
1020  C3%=C2%+X*80:C4%=FNCL(4)
1030  ::VMF C3%,C4%,60
1040 NEXT



# 


#    DISPLAY MAP
1050 ::VMC MA,C1%,MP%(0),MP%(1),80


#    DISPLAY PLAYER POSITION
#    C2%=LINES OFFSET
#    C3%=COLS OFFSET
1100 C2%=PP%/MP%(0)*2
1110 C3%=C2%*80
1115 C4%=PP%-C2%*MP%(0)/2
1120 C1%=C1%+C3%+C4%+C4%
1125 CL%=0
1126 TZ=TI

1130 DO
1135  GET I$:IF LEN(I$)>0 THEN EXIT
1140  IF TI-TZ<30 THEN 1160
1143  CL%=ABS(CL%-1)
1145  C2%=FNCL(CL%*8)
1150  ::VMF C1%,C2%,2
1155  TZ=TI
1160 LOOP

1170 GOSUB340

1999 RETURN



##############################################
# VRAM memory MAP
# 0-7999: double buffer 0 attribute ram
# 8000-15999: double buffer 1 attribute ram
# 16000-40000: "compressed" tiles AND FONT
# 
# 49535-65535: Screen RAM. statically configured for
#              - lowres in the 3d view
#              - maybe something else for text display regions

10000 REM GRAPHIC SETUP

10010 BANK 0

10020 DEF FN PA(ZZ)=7*ZZ+1
10021 DEF FN CL(ZZ)=ZZ*16+ZZ
10025 DEF FN PW(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256
10026 DEF FN M(ZZ)=M%(PP%+V%(FD%,ZZ))
10027 DEF FN FD(ZZ)=INT(FD%+ZZ+4)-INT(INT(FD%+ZZ+4)/INT(4))*INT(4)
10028 DEF FN PP(ZZ)=PP%+P%(FD%+FD%+ZZ)
10029 DEF FN TX(ZZ)=ASC(MID$(TX$,ZZ,1))-32

10030 DD=PEEK(186)
10040 BE=FNPW(4624)
10050 BLOAD"VDCBASIC2D.0AC6",B0,U(DD):SYSDEC("AC6")


10059 VR(0)=65535-160*120:VR(1)=9600:VR(2)=16000:VR(3)=8000:VR(4)=VR(0)+VR(2)
10060 AX=0:AP(0)=0:AP(1)=9600:AP=AP(AX)
10065 ::DISPVR(0)
10066 ::ATTR AP

10070 ::RGW 0,127
10071 ::RGW4,159
#     120 SCREEN-ROWS (2 SCANLINES PER ROW)
10072 ::RGW6,120
# RGW7,140
10073 ::RGW7,133

#     2 SCANLINES PER ROW
10074 ::RGW9,1

10080 ::RGO 25,128:REM BITMAP MODE ON
10090 ::RGO 28,2î¸ž4:REM 64K VRAM
10100 ::RGW 36,0
10110 ::RGW DEC("1A"),DEC("FF")


############################
# SCREEN SETUP
############################
#     16K SCREEN-RAM IS FILLED WITH BG-COLOR LEFT, FG-COLOR RIGHT (TOP 100 SCANLINES)
#      3,2K SCREEN-RAM IS FILLED WITH 0 (BOTTOM 20 SCANLINES)
10120 ::VMF VR(0),15,VR(2)
10124 C1=VR(0)+16000
10125 ::VMF C1,0,3200

#     8000 BYTES ATTRIBUTE RAM ARE FILLED WITH ZERO (LOWRES EMPTY)
#     1600 BYTES ATTRIBUTE RAM ARE SET TO FG/BG COLOR
10130 ::VMF AP,0,VR(3):REM CLEAR ATTRIBUTE RAM
10134 C1=AP+8000
10135 ::VMF C1,DEC("1F"),1600
10140 DA=AP+VR(2)


############################
# LOAD RESOURCES INTO VRAM
############################
10150 FI$="WALLS.VDC"
10160 ?"LOADING "FI$"...";:BLOAD (FI$),B0,P(BE)
10170 FL=FNPW(174)-BE:PRINT FL" BYTES DONE"

10180 DC=PEEK(BE):PRINT DC " ENTRIES"
10185 DIM DM%(DC-1,4)
10190 D=FNPA(DC)
10200 FL=FL-D

#10210 FAST

#33293 -> 10218 ILLEGAL QUANTITY ERROR:FIXED BY INTRODUCING C1 BELOW

#33305 ->       ILLEGAL QUANTITY ERROR
#33622 -> 

#     COPY WALLSET FROM RAM TO VRAM
10215 CA=DA+FL
10216 C1=BE+D
#10219 PRINT "COPYING WALLSET TO VRAM ADDRESS "DA" TO "CA
10220 ::RTV C1,DA,FL



#     WRITE WALLSET-METADATA TO ARRAY
#     0,1:OFFSET
#       2:WIDTH IN BYTES (1 BYTE = 2 PIXELS)
#       3:HEIGHT IN SCANLINES
#       4:SIZE OF TILE (LENGTH IN BYTES)
#     5,6:POSITION OF TILE ON SCREEN
10230 FOR FI=0 TO DC-1
10240  PA=BE+FNPA(FI)
10250  DM%(FI,0)=FNPW(PA)
10260  DM%(FI,1)=PEEK(PA+2)
10270  DM%(FI,2)=PEEK(PA+3)
10280  DM%(FI,3)=FNPW(PA+4)
10290  DM%(FI,4)=PEEK(PA+6)+406
10310 NEXT


10311 PRINT"LOADING FONT...";:BLOAD"FONT2.VDC",U(DD),B0,P(BE)
10312 FL=FNPW(174)-BE:PRINT FL" BYTES DONE":MA=CA+FL
10313 PRINT "COPYING FONTS TO VRAM ADDRESS "CA
10314 ::RTV BE,CA,FL

10320 PRINT "MAP ADDRESS IN VRAM STARTING AT "MA
10325 PRINT "SCREEN-RAM STARTING AT"VR(0)

# TD: TEXT-DELTA (OFFSET IN PIXELS TO TEXT-START)
10400 TX$="GREETINGS, CRAWLER!":TD=330:GOSUB 800

10410 FD$="NESW"

10980 BANK 15
10990 RETURN

# MAP DATA
# ODD NUMBERS ARE NOT-WALKABLE
# 1 - REGULAR WALL
# 3 - WALL OF MONSTER DEN
# 5 - DOOR OF MONSTER DEN

# EVEN NUMBERS ARE
# 0 - EMPTY
# 2 - COIN
# 4 - POWER COIN
# 6 - SPAWN POINT
# 8 - TELEPORT TO A
# A - TELEPORT TO 8


#60000 DATA 1,1,1,1,1,1,1,1
#60001 DATA 1,2,2,2,2,2,2,1
#60002 DATA 1,2,1,1,2,1,2,1
#60003 DATA 1,2,2,0,6,2,2,1
#60004 DATA 1,2,2,0,0,2,2,1
#60005 DATA 1,2,1,2,1,1,2,1
#60006 DATA 1,2,2,2,2,2,2,1
#60007 DATA 1,1,1,1,1,1,1,1


#60000 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
#60001 DATA 1,6,2,2,2,1,2,2,2,2,2,2,2,2,1,2,2,2,2,1
#60002 DATA 1,2,1,1,2,1,2,1,1,1,1,1,1,2,1,2,1,1,2,1
#60003 DATA 1,2,1,2,2,2,2,2,2,2,2,2,2,2,2,2,2,1,2,1
#60004 DATA 1,2,1,2,1,1,2,1,1,0,0,1,1,2,1,1,2,1,2,1
#60005 DATA 1,2,2,2,2,2,2,1,0,0,0,0,1,2,2,2,2,2,2,1
#60006 DATA 1,2,1,2,1,1,2,1,1,1,1,1,1,2,1,1,2,1,2,1
#60007 DATA 1,2,1,2,2,2,2,2,2,2,2,2,2,2,2,2,2,1,2,1
#60008 DATA 1,2,1,1,2,1,2,1,1,1,1,1,1,2,1,2,1,1,2,1
#60009 DATA 1,2,2,2,2,1,2,2,2,2,2,2,2,2,1,2,2,2,2,1
#60010 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1

60100 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
60101 DATA 1,2,2,2,2,2,2,2,2,2,2,2,2,1,1,2,2,2,2,2,2,2,2,2,2,2,2,1
60102 DATA 1,2,1,1,1,1,2,1,1,1,1,1,2,1,1,2,1,1,1,1,1,2,1,1,1,1,2,1
60103 DATA 1,4,1,1,1,1,2,1,1,1,1,1,2,1,1,2,1,1,1,1,1,2,1,1,1,1,4,1
60104 DATA 1,2,1,1,1,1,2,1,1,1,1,1,2,1,1,2,1,1,1,1,1,2,1,1,1,1,2,1
60105 DATA 1,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,1
60106 DATA 1,2,1,1,1,1,2,1,1,2,1,1,1,1,1,1,1,1,2,1,1,2,1,1,1,1,2,1
60107 DATA 1,2,1,1,1,1,2,1,1,2,1,1,1,1,1,1,1,1,2,1,1,2,1,1,1,1,2,1
60108 DATA 1,2,2,2,2,2,2,1,1,2,2,2,2,1,1,2,2,2,2,1,1,2,2,2,2,2,2,1
60109 DATA 1,1,1,1,1,1,2,1,1,1,1,1,0,1,1,0,1,1,1,1,1,2,1,1,1,1,1,1
60110 DATA 1,1,1,1,1,1,2,1,1,1,1,1,0,1,1,0,1,1,1,1,1,2,1,1,1,1,1,1
60111 DATA 1,1,1,1,1,1,2,1,1,0,0,0,0,0,0,0,0,0,0,1,1,2,1,1,1,1,1,1
60112 DATA 1,1,1,1,1,1,2,1,1,0,3,3,3,5,5,3,3,3,0,1,1,2,1,1,1,1,1,1
60113 DATA 1,1,1,1,1,1,2,1,1,0,3,0,0,0,0,0,0,3,0,1,1,2,1,1,1,1,1,1
60114 DATA 0,0,8,0,0,6,2,0,0,0,3,0,0,0,0,0,0,3,0,0,0,2,0,0,0,8,0,0
60115 DATA 1,1,1,1,1,1,2,1,1,0,3,0,0,0,0,0,0,3,0,1,1,2,1,1,1,1,1,1
60116 DATA 1,1,1,1,1,1,2,1,1,0,3,3,3,3,3,3,3,3,0,1,1,2,1,1,1,1,1,1
60117 DATA 1,1,1,1,1,1,2,1,1,0,0,0,0,0,0,0,0,0,0,1,1,2,1,1,1,1,1,1
60118 DATA 1,1,1,1,1,1,2,1,1,0,1,1,1,1,1,1,1,1,0,1,1,2,1,1,1,1,1,1
60119 DATA 1,1,1,1,1,1,2,1,1,0,1,1,1,1,1,1,1,1,0,1,1,2,1,1,1,1,1,1
60120 DATA 1,2,2,2,2,2,2,2,2,2,2,2,2,1,1,2,2,2,2,2,2,2,2,2,2,2,2,1
60121 DATA 1,2,1,1,1,1,2,1,1,1,1,1,2,1,1,2,1,1,1,1,1,2,1,1,1,1,2,1
60122 DATA 1,2,1,1,1,1,2,1,1,1,1,1,2,1,1,2,1,1,1,1,1,2,1,1,1,1,2,1
60123 DATA 1,4,2,2,1,1,2,2,2,2,2,2,2,0,0,2,2,2,2,2,2,2,1,1,2,2,4,1
60124 DATA 1,1,1,2,1,1,2,1,1,2,1,1,1,1,1,1,1,1,2,1,1,2,1,1,2,1,1,1
60125 DATA 1,1,1,2,1,1,2,1,1,2,1,1,1,1,1,1,1,1,2,1,1,2,1,1,2,1,1,1
60126 DATA 1,2,2,2,2,2,2,1,1,2,2,2,2,1,1,2,2,2,2,1,1,2,2,2,2,2,2,1
60127 DATA 1,2,1,1,1,1,1,1,1,1,1,1,2,1,1,2,1,1,1,1,1,1,1,1,1,1,2,1
60128 DATA 1,2,1,1,1,1,1,1,1,1,1,1,2,1,1,2,1,1,1,1,1,1,1,1,1,1,2,1
60129 DATA 1,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,1
60130 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1



#60070 DATA -20,20,1,-1,20,-20,-1,1
62070 DATA -28,28,1,-1,28,-28,-1,1

62080 DATA 1,3,8, 2,5,12, 6,9,15, 7,11,19, 13,16,0, 14,18,0
