#RetroDevStudio.MetaData.BASIC:7169,Walrus BASIC 8,uppercase,10,10
# MOVEMENT DATA
1 DATA -8,8,1,-1,8,-8,-1,1

# 2D-3D TRANSFORMATION DATA
2 DATA -1,1,-9,-8,-7,-9,-7,-17,-17,-16,-15,-15,-17
3 DATA -15,-25,-25,-24,-23,-23,-8
4 DATA 8,-7,1,9,-7,9,-6,-6,2,10,10,-6,10,-5,-5,3
5 DATA 11,11,1,-1,9,8,7,9
6 DATA 7,17,17,16,15,15,17,15,25,25,24,23,23,8,-8,7,-1,-9,7
7 DATA -9,6,6,-2,-10
8 DATA-10,6,-10,5,5,-3,-11,-11

# MAP DATA
10 DATA 1,1,1,1,1,1,1,1, 1,0,0,0,0,0,0,1, 1,0,0,1,1,0,0,1, 1,0,0,0,0,0,0,1
11 DATA 1,0,1,0,1,1,0,1, 1,0,1,0,1,1,0,1, 1,0,0,0,0,1,0,1, 1,1,1,1,1,1,1,1

# SCREEN COORDINATES FOR DRAWING
20 DATA 0,0,0,1,1,1,7,0,8, 0,8,0,7,1,7,7,8,8, 1,0,1,1,1,1,7,0,7, 1,1,1,7,1,7,7,1,7
21 DATA 1,8,1,7,1,7,7,8,7, 0,1,1,2,2,2,6,1,7, 0,7,1,6,2,6,6,7,7, 0,0,2,1,2,1,6,0,6
22 DATA 0,1,2,2,2,2,6,1,6, 0,2,2,6,2,6,6,2,6, 0,7,2,6,2,6,6,7,6, 0,8,2,7,2,7,6,8,6
23 DATA 0,2,2,3,3,3,5,2,6, 0,6,2,5,3,5,5,6,6, 0,1,3,2,3,2,5,1,5, 0,2,3,3,3,3,5,2,5
24 DATA 0,3,3,5,3,5,5,3,5, 0,6,3,5,3,5,5,6,5, 0,7,3,6,3,6,5,7,5

25 @WALRUS,0

30 P=26:DEFFNM(Z)=M(P)+V(F,Z)>0:DEFFNMO(Z)=INT(Z)-INT(INT(Z)/INT(4))*INT(4)

35 @MODE,0:@SCREEN,0:@COLOR,0,15,0:@CLEAR,0:

36 @CHAR,254,3,192,1,1,2," TO OVE,  TO XIT":@WINDOWOPEN,0,16,256,164,1

37 DIMV(3,19),D(19,8),M(63)

#  READ POSITION CHANGE DATA FOR MOVEMENT
38 FORX=0TO3:FORY=0TO1:READM:P(X,Y)=M:NEXT:NEXT

#  READ 2D-3D TRANSFORMATION DATA
40 FORY=0TO3:FORX=1TO19:READM:V(Y,X)=M:NEXT:NEXT

#  READ MAP DATA
42 FORX=0TO63:READM:M(X)=M:NEXT

#  READ SCREEN COORDINATES FOR DRAWING
45 FORX=1TO19:FORY=0TO8:READ M:D(X,Y)=M*16:NEXT:NEXT
46 GOTO70

50 GETKEYI$:I=ASC(I$):F=FNMO(F+(I=81)+ABS(I=69)+4):IFI=65ORI=68THENMD=ABS(I=68):F=FNMO(F+3):GOSUB150:F=FNMO(F+5)

60 IFI=87ORI=83THENMD=ABS(I=83):GOSUB150:ELSEIFI=88THEN@TEXT:END

70 R=0:C$="ACH":GOSUB120:C$="BEL":GOSUB120:GOSUB80:C=0:GOSUB140:GOTO160

80 IFFNM(4)THENC=4:GOSUB140:RETURN

90 C$="FIO":GOSUB120:C$="GKS":GOSUB120:GOSUB100:RETURN

100 IFFNM(10)THENC=10:GOSUB140:RETURN

110 C$="MP":GOSUB120:C$="NR":GOSUB120:IFFNM(17)THENC=17:GOSUB140:RETURN:ELSERETURN

120 CX=1:DO:C=ASC(MID$(C$,CX,1))-64:S=FNM(C):IFSTHENGOSUB140

130 CX=CX+1:IFCX>LEN(C$)THENEXIT:ELSELOOPWHILES>=0:RETURN

140 R(R)=C:R=R+1:RETURN

150 T=P:P=P+P(F,MD):IFM(P)>0THENP=T:RETURN:ELSERETURN

160 R=0:@CLEAR,0:@CHAR,254,16,8,1,1,2,MID$("",F+1,1)+STR$(P):DO

170 D=R(R):IF D(D,0)=1 THEN 177

# DRAW POLYGON
172 @LINE,D(D,1),D(D,2),0,D(D,3),D(D,4),0,1
173 @LINE,D(D,3),D(D,4),0,D(D,5),D(D,5),0,1
174 @LINE,D(D,5),D(D,6),0,D(D,7),D(D,6),0,1
175 @LINE,D(D,7),D(D,8),0,D(D,1),D(D,2),0,1
176 GOTO 179

# DRAW BOX
177 @BOX,D(D,1),D(D,2),0,D(D,5),D(D,6),0,0,0,1


179 @CHAR,254,0,128*R,1,1,2,STR$(D)

180 R=R+1:LOOPWHILED>0:GOTO50
